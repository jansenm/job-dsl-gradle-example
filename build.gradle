plugins {
    id('groovy')
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

sourceSets {
    jobs {
        groovy {
            srcDirs 'src/jobs'
            compileClasspath += main.compileClasspath
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

repositories {
    mavenCentral()
    // maven { url 'https://repo.jenkins-ci.org/releases/' }
    maven { url 'https://repo.jenkins-ci.org/public/' }
}

configurations {
    testPlugins {}

    // see JENKINS-45512
    testCompile {
        exclude group: 'xalan'
        exclude group: 'xerces'
    }
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
configurations.all*.exclude group: 'xalan'

dependencies {
    implementation platform('org.codehaus.groovy:groovy-bom:2.4.21')
    implementation 'org.codehaus.groovy:groovy'

    testImplementation platform("org.spockframework:spock-bom:1.3-groovy-2.4")
    testImplementation "org.spockframework:spock-core"

    implementation "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"

    // Jenkins test harness dependencies
    testImplementation("org.jenkins-ci.main:jenkins-test-harness:${jenkinsTestHarnessVersion}") {
        exclude group: 'org.netbeans.modules', module: 'org-netbeans-insane' // https://github.com/sheehan/job-dsl-gradle-example/issues/90
    }
    testImplementation("org.jenkins-ci.main:jenkins-war:${jenkinsVersion}") {
        exclude group: 'org.jenkins-ci.ui', module: 'bootstrap' // https://github.com/sheehan/job-dsl-gradle-example/issues/87
    }

    // Job DSL plugin including plugin dependencies
    testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    // testImplementation 'org.jenkins-ci.plugins:structs:338.v848422169819@jar'
    testImplementation "org.jenkins-ci.plugins:structs:${structsVersion}@jar"

    // Run the following script in the Script Console of your Jenkins instance to generate
    // the above testPlugins list. (adapted from https://git.io/fjpUs)
    /*
        Jenkins.instance.pluginManager.plugins
            .findAll { !(it.shortName in ['job-dsl', 'structs']) }
            .collect { "testPlugins '${it.manifest.mainAttributes.getValue("Group-Id")}:${it.shortName}:${it.version}'" }
            .sort()
            .each { println it }
     */

    // Plugins to install in test instance
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.1012.v79a_86a_1ea_c1f'
    testPlugins 'org.jenkins-ci.plugins:credentials:1415.v831096eb_5534'
    testPlugins 'org.jenkins-ci.plugins:cvs:483.v69a_98dfd973b_'
    testPlugins 'org.jenkins-ci.plugins:token-macro:444.v52de7e9c573d'
    testPlugins 'io.jenkins.plugins:pipeline-groovy-lib:752.vdddedf804e72'

}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'
    def mapping = [:]

    doFirst {
        configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
            mapping[it.file.name] = "${it.name}.${it.extension}"
        }
    }
    rename { mapping[it] }

    doLast {
        List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    jvmArgs("--add-opens=java.base/java.util=ALL-UNNAMED",
            "--add-opens=java.base/java.lang=ALL-UNNAMED",
            "--add-opens=java.base/java.lang.invoke=ALL-UNNAMED")

    testLogging {
        events "passed", "skipped", "failed"
    }
}
